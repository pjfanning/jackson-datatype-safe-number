plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group = 'com.github.pjfanning'
version = '2.15.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    implementation 'com.github.pjfanning:safe-number-parser:0.1.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

task createBuildProperties()  {
    doLast {
        def packageDir = new File(sourceSets.main.output.resourcesDir, "com/github/pjfanning/jackson/safenumber")
        packageDir.mkdirs()
        new File(packageDir, "build.properties").text =
                """version: $version
groupId: ${rootProject.group}
artifactId: ${rootProject.name}
"""
    }
}

jar.dependsOn(createBuildProperties)

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'com.github.pjfanning.jackson.safenumber')
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'jackson-datatype-safe-number'
                description = 'Jackson bindings for Safe Number lib'
                url = 'https://github.com/pjfanning/jackson-datatype-safe-number'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pjfanning'
                        name = 'PJ Fanning'
                    }
                }
                scm {
                    url = 'https://github.com/pjfanning/jackson-datatype-safe-number'
                    connection = 'scm:git://github.com/pjfanning/jackson-datatype-safe-number.git'
                    developerConnection = 'scm:git://github.com/pjfanning/jackson-datatype-safe-number.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}